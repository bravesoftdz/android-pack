include("General")
include("ViewCommon")

func Init()
  fvar(old, tmp)

  AddToImport('android.widget.Spinner')
  AddToImport('android.widget.ArrayAdapter')
  AddToImport('android.widget.AdapterView')
  AddToImport('android.view.View')
  AddToImport('android.widget.TextView')
  AddVar(spn, 'Spinner')

  tmp = block.reggen()
  old = block.select(tmp)

  println('<Spinner android:id="@+id/',spn,'"')
  PrintCommonLayoutAttributes()
  println('android:spinnerMode="',lower(SpinnerMode),'"')

  //================= Генерация файла пунктов списка =========================//
  if(isndef(Items))
    fvar(old2, i)

    block.reg(spn_items)
    ARR_VALUES &= spn_items
    old2 = block.select(spn_items)

    println('<?xml version="1.0" encoding="utf-8"?>')
    println('<resources>')
      block.inclvl()
        println('<string-array name="',spn_items,'">')
          block.inclvl()
            for(i = 0; i < _arr.count(Items); i++)
              println('<item>',lng.strtoutf8(lng.strtoxml(Items[i])),'</item>')
            end
          block.declvl()
        println('</string-array>')
      block.declvl()
    println('</resources>')

    block.select(old2)
    println('android:entries="@array/',spn_items,'"')
  end
  //==========================================================================//
  println('/>')

  if (LayoutManager <> 0)
    sys.event(LayoutManager, 'add_to_layout', tmp, Top, Left)
  else
    sys.event(PARENT_ID, 'add_to_layout', tmp, Top, Left)
  end

  if (ContextMenu <> 0)
    sys.event(ContextMenu, 'add_component', spn)
  end
  

  block.select(BLK_ON_CREATE)
    println(spn, ' = (Spinner) findViewById(R.id.',spn, ');')

    if(isndef(Prompt))
      println(spn,'.setPrompt(',e_str(Prompt),');')
    end

    if(linked(onItemSelected))
      tmp = block.reggen()
      block.select(tmp)
      println(spn,'.setOnItemSelectedListener(new AdapterView.OnItemSelectedListener() {')
        block.inclvl()
          println('public void onItemSelected(AdapterView<?> ',parent,', View ',view,', int ',position,', long ',id,') {')
            block.inclvl()
              event(onItemSelected, position)
            block.declvl()
          println('}')
          println('public void onNothingSelected(AdapterView<?> parent) {')
            block.inclvl()
              event(onNothingSelected)
            block.declvl()
          println('}')
        block.declvl()
      println('});')
      MoveBlockTo(BLK_ON_CREATE)
    end

  block.select(old)
end

func doItems()
  fvar(l)
  
  l = ReadListData(ItemsList, 2, '')
  if(l = '')
    traceError('ItemsList', 'List<String> или List<String>', ItemsList, 'doItems')
    return(0)
  end
  
  println(spn,'.setAdapter(new ArrayAdapter(',CURRENT_CLASS,'.this, android.R.layout.simple_spinner_dropdown_item, ', l,'));')
end

func doPrompt(_data)
  println(spn,'.setPrompt(',e_str(PromptText),');')
end

func doSelectItem(_data)
  println(spn,'.setSelection(',e_int(_data),');')
end

func Index()
  return(spn + '.getSelectedItemPosition()')
end

func Count()
  return(spn + '.getCount()')
end